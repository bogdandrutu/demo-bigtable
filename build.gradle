plugins {
    id 'com.github.sherter.google-java-format' version '0.6'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

def grpcVersion = '1.10.0'
def guavaVersion = '19.0'
def bigtableVersion = '1.3.0'
def opencensusVersion = '0.13.2'

dependencies {
    compile ("com.google.cloud.bigtable:bigtable-hbase-1.x:${bigtableVersion}") {
        // we'll always be more up-to-date
        exclude group: 'io.grpc', module: 'grpc-context'
        exclude group: 'io.grpc', module: 'grpc-core'
    }
    compile "com.google.guava:guava:${guavaVersion}"
    compile "io.grpc:grpc-core:${grpcVersion}"
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "io.opencensus:opencensus-api:${opencensusVersion}"
    compile "io.opencensus:opencensus-contrib-grpc-metrics:${opencensusVersion}"
    compile "io.opencensus:opencensus-exporter-trace-stackdriver:${opencensusVersion}"
    compile "io.opencensus:opencensus-exporter-stats-stackdriver:${opencensusVersion}"

    runtime "io.opencensus:opencensus-impl:${opencensusVersion}"
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

googleJavaFormat {
    toolVersion '1.5'
}

afterEvaluate {  // Allow subproject to add more source sets.
    tasks.googleJavaFormat {
        source = sourceSets*.allJava
        include '**/*.java'
    }

    tasks.verifyGoogleJavaFormat {
        source = sourceSets*.allJava
        include '**/*.java'
    }
}

startScripts.enabled = false

task demoApp(type: CreateStartScripts) {
    mainClassName = 'com.google.e2edebugging.DemoAppMain'
    applicationName = 'demo-app-main'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into('bin') {
    from(demoApp)
    fileMode = 0755
}
